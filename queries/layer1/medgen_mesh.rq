# Endpoint: https://rdfportal.org/ncbi/sparql
PREFIX mo: <http://med2rdf/ontology/medgen#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mesh: <http://id.nlm.nih.gov/mesh/vocab#>

SELECT DISTINCT ?medgen_id ?mesh_id ?mesh_tree_id
FROM <http://rdfportal.org/dataset/medgen>
FROM <http://id.nlm.nih.gov/mesh>
WHERE {
  ?medgen_uri
    dct:identifier ?medgen_id ;
    mo:mgconso ?mgconso .
  ?mgconso
    dct:source mo:MSH ;
    rdfs:seeAlso ?mesh .
  ?mesh mesh:treeNumber ?mesh_tree .
  BIND (strafter(str(?mesh), 'http://id.nlm.nih.gov/mesh/') AS ?mesh_id)
  BIND (STRAFTER (STR (?mesh_tree), 'http://id.nlm.nih.gov/mesh/') AS ?mesh_tree_id)
}
